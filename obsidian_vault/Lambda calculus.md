Лямбда исчисление определяется тремя конструкциями:
1. Переменная `x`, `y` и тд. 
2. "Абстракция", или функция/лямбда `fn x -> body`. Функции всегда объявляют одну переменную, которую можно использовать в теле функции. 
3. "Применение абстракции" или вызов функции, обозначаются двумя переменными подряд `x y`. Вызов функций лево-асоциативный, что значит `x y z = (x y) z` 

Данные конструкции определяют достаточно простой, но очень експресивный синтаксис. Доказано что эта система является еквивалентной машине Тьюринга.

Я даже уже представляю как описать конструкции структурного программирования в лямбда исчислении. Структурное программирование определяется так же тремя конструкциями:
1. Последовательность инструкций
2. Условный оператор, ака if statements 
3. While циклы.

Каждую из конструкций можно описать в лямбда исчислении по отдельности с помощью монад, булеанов и рекурсии.

На деле имена переменным не обязательны, и можно обойтись "индексом" - насколько далеко переменная от ее объявления. Тоесть насколько функций вверх надо смотреть, что бы найти функцию которая объявляет эту переменную.
Например `fn x -> x` тогда можно записать как `fn -> #0`. Префикс в виде `#` желателен что бы отличать эти индексы от обычных чисел.

Так же определяется так называемая бета-редукция (beta-reduction). У нормальных людей это обычно называют подстановкой параметра в тело функции. После подстановки вместо функции и аргумента ставится результат подстановки. Таким образом выражение `(fn x -> x) y` упрощается до просто `y`

Эта система позволяет определят алгебраические типы данных. Тоесть енамы, каждый вариант которого может держать несколько вложенных значений дополнительно. В расте самый приятный синтаксис дл объявления таких структур, поэтому вот пару примеров:

```rust
enum Expression {
  Value(i32),
  Product(Expression, Expression),
  Sum(Expression, Expression),
}

enum Color {
  Red,
  Green,
  Blue
}

enum Boolean {
  True,
  False
}

enum Integer {
  Zero,
  Successor(Integer)
}
```

Как такие значения можно определить в лямбда исчислении? Для начала определимся как репрезентовать такие значения можно. А для этого подумаем как мы хотим вообще использовать такие значения. В том же расте у нас есть match оператор, который выглядит вот так:

```rust
match boolean {
  True => Successor(Zero),
  False => Zero
}
```

Читается как *если переменная boolean держит вариант True, вернуть Successor(Zero), если False, то вернуть Zero*

Это поведение является определяющим для енамов.
Если убрать весь синтаксис по сути остаётся только `boolean`, `Successor(Zero)`, и просто `Zero`, которые надо как то скомбинировать и получить то же поведение.
Но так или иначе все эти значения должны быть функциями в рамках лямбда исчисления, а значит можем просто передать в `boolean` наши два значения. Тоесть еквивалентное этому метчу выражение будет выглядеть так `boolean (Successor(Zero)) Zero`. 
Это уже что-то. Из этого можем вывести уже что `boolean` это функция двух переменных. И так можно описать все подобные енамы - "значения" такого типа выглядят как функции которые принимают по одному параметру на каждый вариант даного типа. Тогда если хотим получить желаемое поведение, то имеем что `true` это булеан который возвращает первый аргумент, а `false` - который возвращает второй:

```
true = fn trueMatch -> fn falseMatch -> trueMatch
false = fn trueMatch -> fn falseMatch -> falseMatch
```

Интуитивно я воспринимаю эти константы как "метчеры", потому что в рамках такого оператора как match они полностью выполняют его функцию. Из этой логики и вытекает репрезентация описанная выше.

А теперь рассмотрим такой метч с числом:

```rust
match integer {
  Successor(anotherNumber) => anotherNumber,
  Zero => Zero
}
```

Читается как *если число является следующим после другого числа, то возвращаем это другое число, если это ноль, то возвращаем ноль*. По сути возвращает integer - 1

В этот раз у нас всё выглядит немного иначе. У нас есть вариант `Successor` "содержащий" в себе другое число, которое мы достаем в соответствующей ветке. Раз уж мы не знаем какое число это должно быть и где его взять, эту ветку можно описать только функцией, которая принимает число из этого варианта енама. Тогда выражение в лямбда исчислении будет выглядеть так: `integer (fn anotherNumber => anotherNumber) zero`
Из этого имеем что значение `integer` так же должно иметь вид функции двух переменных, как и `boolean`, но с отличием что первый аргумент тоже всегда должен быть функцией. 
Со второй же веткой метча всё и так понятно, значение `integer` которое будет так работать выглядит буквально как `false` из предыдущего примера.

А вот что делать с веткой `Successor`? Откуда мы возьмём число что бы передать его в ветку? Ну для начала что бы создать значение этого варианта тоже нужно иметь число. Это не просто константа, это результат "конструктора" этого значения. Этот конструктор принимает число и отдает новое число. Звучит какраз как то чего нам не хватало, что бы описать метч второй ветки. Мы можем передать это число которое получили в конструкторе в соответственную ветку метча.
В таком случае варианты выглядят так:

```
successor = fn predecessor -> fn successorMatch -> fn zeroMatch -> successorMatch predecessor
zero = fn successorMatch -> fn zeroMatch -> zeroMatch
```

Следуя такой логике мы можем определить например кортежи (tuple)

Кортежи это тип данных с одним вариантом для метча и двумя параметрами нужных для его создания, которые метчер должен передавать. Следовательно "значения" это функции одного аргумента, и "конструктор" это функция двух аргументов, что имеет примерно такой вид:

`tuple = fn first -> fn second -> fn matched -> matched first second`

Метчингом можно определить функции которые берут первый параметр их кортежа и второй соответственно:

```
first = fn tuple -> tuple (fn first -> fn second -> first)
second = fn tuple -> tuple (fn first -> fn second -> second)
```

И что интересно, метчеры в функциях `first`, `second` выглядят в точности как `true`, `false`, описанные ранее, тоесть их можно переписать так:

```
first = fn tuple -> tuple true
second = fn tuple -> tuple false
```

Имея эти все базовые блоки уже можно кучу всего полезного определить, например операции над числами (`add`, `sub`, `mult`, etc.), логику на булеанах (`and`, `or`, `not`, `xor`, etc.)

Дальше добавлю немного сахара, что бы было компактнее:
1. `(a, b) = tuple a b`
2. `tuple[0] = first tuple, tuple[1] = second tuple`
3. `id = () = fn x -> x`
4. `0 = zero, 1 = successor zero, 2 = successor successor zero, ...`
5. `if bool then body1 else body2 = bool body1 body2`
6. `if bool then body1 = if bool then body1 else ()`

И в добавок к этому блок определений переменных: 
`{ x = v1; y = v2; result } = (fn x -> (fn y -> result) v2) v1`

Такие блоки так же позволяют "игнорировать" результат и "перекрывать" ранее обьявленные переменные:

`{ x = v1; v2; x = v3; ... } = (fn x -> (fn _ -> (fn x -> ...) v3) v2) v1`

Для монады надо определить две вещи - способ создания "монадных" значений `value` (или просто обёртка над обычными значениями) и способ мапать то что внутри в новое обернутое значение `flatMap`. Имея это, блоки кода можно "декорировать" такой монадой, и таким образом автоматически "разворачивать" значения внутри монад:

```
{ x = v1; y = v2; result } 
= flatMap (fn x -> flatMap (fn y -> result) v2) v1
```

Монады не панацея - они ожидают что так или иначе каждое выражение в последовательности возвращает уже обернутое значение, иначе `flatMap` не будет корректно работать. Даже `result` который наприямую не передается в `flatMap` должен быть обернут, потому что иначе `fn y -> result` не соответствует контракту.
Альтернативно, можно вводить новый оператор `unwrap` в таких блоках, который позволяет разворачивать значения:

```
{ x = v1; y = unwrap v2; result } 
= (fn x -> flatMap (fn y -> result) v2) v1
```

В таком случае мы убираем требование что все выражения должны возвращать обернутое значение, но теперь нужно явно разварачивать их.
Например допустим есть тип `Option` с вариантами `Some(x)` и `None`, определенными аналогично целым числам (без типов и имен параметров они буквально одинаковые).
Для того что бы option называть монадой достаточно определить `flatMap`, который будет мапать а потом выравнивать вложенные `Option`.

```
value = Some
flatMap = fn mapper -> fn option -> option mapper None = fn mapper -> fn option -> flat (map mapper option)
map = fn mapper -> fn option -> option (fn value -> Some(mapper value)) None
id = fn value -> value
flat = fn option -> option id None
```

Концептуально маппер переданный в `flatMap` описывает "продолжение вычисления", а сама функция есть выполнение одного шага в этом вычислении.

Оператор для разворачивания часто можно автоматически вставлять на основе типов как вид каста - каждый раз когда ожидается параметр типа `Т`, а туда передается `Option<T>`, компилятор автоматически вставляет "каст" из `Option<T>` в `T`, таким образом разворачивая значение. При этом сама функция теперь будет возвращать результат обернутый в `Option<...>`, ведь если значения небыло то придется сделать early return. Если же значение обернуто в несколько монад, например `Promise<Option<T>>`, то компилятору придется вычислять последовательность кастов, или же пользователи сами должны будут указать каким то образом как кастовать `Promise<Option<T>>` в `T`.

Думаю достаточно легко найти сходство любой монады с классическими блоками кода. Но недостаток монад в том что они "красят" блоки кода в свой "цвет", и заставляют блоки которые их используют тоже становится того же цвета, потому что иначе не удобно (а иногда невозможно) пользоваться результатом такого выражения. В примере выше это было буквально видно - если блок хочет "развернуть" значение из монады, то результирующее значение тоже станет обернутым в монаду, и теперь если получатель значения хочет без усложнений в виде явной обработки возможных кейсов воспользоваться результатом, его результат тоже будет обернут в `Option<...>`.

С помощью монад можно реализовать стандартный блок кода с `break` оператором, который позволяет выйти из блока раньше чем он закончится. Этот оператор будет принимать значение, которое будет считаться результатом блока. И так же блоки кода должны позволять определить переменные инициализированные некоторым значением на время жизни блока. Такие конструкции позволяют определить базовый control flow в выражении.
Какая монада позволит нам такое реализовать? Ну давайте опишем результат каждого шага в таком блоке в виде енама:

```
enum BlockState {
	Next(value),
	Break(withValue)
}
```

Можно сказать мы каждую из команд доступных в блоке шифруем в виде одного из вариантов енама со всеми данными требуемыми для выполнения этой команды.
И как мы помним такой енам конвертируется в следующие конструкторы:

```
BlockState_Next = fn value -> fn nextMatch -> fn breakMatch -> nextMatch value
BlockState_Break = fn withValue -> fn nextMatch -> fn breakMatch -> breakMatch withValue
```

Но эта штука должна быть монадой, что бы мы ее могли использовать с выше описаным синтаксисом, а значит определяем `value` и `flatMap` для него:

```
value = BlockState_Next
flatMap = fn mapper -> fn blockState -> blockState mapper id
```

По сути логика такова получается:
- Если `blockState` это `break`, то возвращаем то что лежало внутри
- Если `blockState` это `next`, то продолжаем цепочку вычислений с тем что лежало внутри

Если сделать алиас `break = BlockState_Break`, то поведение будет выглядить почти как в Rust:

```
{ 
	x = v1; 
	y = v2; 
	if y then break result2 else value ();
	result 
} = flatMap (fn x -> flatMap (fn y -> flatMap (fn _ -> result) (if y then break result2 else value ())) v2) v1
```

Если же проводить анализ каждого выражения, и добавлять обертку в `value` где ее нету, что бы приводить результаты всех выражений к единому интерфейсу, то приходим к виду как в Rust: 

```
{ x = v1; y = v2; if y then break result2; result } = 
	flatMap (fn x -> flatMap (fn y -> flatMap (fn _ -> result) (if y then break result2 else value ())) v2) v1
```

Теперь перейдем к тому как можно реализовать циклы в подобной системе. Разсмотрим следуйщий пример из Python:

```
while test_condition():
    do_stuff()
```

Выглядит просто? Впринципе да. Но в примере `test_condition` и `do_stuff` это функции, которые вызываются и вероятно меняют какое то общее состояние, иначе это достаточно бесполезный цикл, который или вечный или вообще никогда не запускается. Сделаем это общее состояние явным:

```
while test_condition(state):
    state = do_stuff(state)
```

При этом ожидается, что теперь `test_condition` и `do_stuff` не имеют общего состояния где то вне этого блока - всё состояние которое они требуют передается им явно и `test_condition` возвращвает условие, а `do_stuff` новое состояние. Даже если `test_condition` изначально тоже делает часть изминений в состоянии всегда можно разбить на две части его - одна которая вычисляет только условие, и вторая которая делает все изминения в состоянии.
Запишем в виде функции в лямбда исчислении:

```
while = fn condition -> fn body -> fn state ->
	if (condition state) 
	then while condition body (body state)
	else state
```

Хм... виднеется проблема. Наше определение вызывает самого себя, хотя по факту лямбда исчисление не позволяет вот так просто ссылаться на определяемую функцию. Но по сути это всеравно что если бы функция ещё одним аргументом принимала "саму себя", и мы просто сразу на месте дали ей копию себя. И эту конструкцию даже можно абстрагировать в отдельную функцию, что бы не приходилось дублировать тело основной функции. В реальности выглядит примерно так:

```
pass_self = fn f -> f f
while = fn condition -> fn body -> 
	pass_self (
		fn self -> fn state ->
			if (condition state) 
			then self self (body state)
			else state
	)
```

Хотя этого впринципе достаточно для функционирования, это не очень красиво выглядит, ведь всеравно приходится в теле передавать самого себя, хотелось бы принимать self с уже переданным самим собой, тогда можно более менее прямо описывать рекурсию. Для этого можем добавить ещё одну вспомогательную функцию, и тогда реализация становится максимально чистой:

```
pass_self_clean = fn f -> pass_self (fn self -> f (self self))
while = fn condition -> fn body -> 
	pass_self_clean (
		fn self -> fn state ->
			if (condition state) 
			then self (body state)
			else state
	)
```

В итоге сама функция принимае три параметра - условие, тело, и начальное общее состояние между ними двумя.
Утилита `pass_self_clean` которая в итоге получилась имеет имя Y-combinator или fixed-point combinator, и обычно обозначается `fix`. Первое имя на самом деле не представляю кто придумал, а второе уходит корнями в математику. 
Данный комбинатор по сути определяет значение `x` (фиксированную точку функции `f`), для которой справедливо равенство `x = f x`, тоесть даже если ещё раз применить переданную функцию к результату, мы просто получим тоже самое. 
В рамках while оператора это означает что на выходе мы всегда получаем `state` для которого тело оператора возвращает это же состояние, что как видно из реализации возможно только если условие для цикла уже недействительно.

Вместе с этим можно считать, что мы реализовали основные примитивы структурного програмирования, так привычные с императивных языков.

Дальше я планирую так же описать типизированное лямбда исчисление на основе исчисления конструкций, где мы пройдемся по всем этим конструкциям и дадим каждой из них тип. Сразу как только я пойму как это дерьмо вообще должно работать.

PS. в качестве бонуса: тип чисел который был определен почти в самом начале `Integer` по сути является фиксированной точкой для `Option<T>`, что станет видно если провернуть то, что мы сделали с `while`, с `Integer`.

PSS. А можно ли определить как то взаимно рекурсивные функции? Да, можно. По сути сводится к тому что бы определить вариант енама под каждую функцию которую мы хотим иметь возможность рекурсивно вызывать. Класический пример - пара функций `even` и `odd`:

```
even = fn n -> if n = 0 then true else odd (n - 1)
odd = fn n -> if n = 1 then true else even (n - 1)
```

Что бы определить не взаимнорекурсивные функции достаточно сделать одну общую функцию, которая принимает доп параметр, по которому определяет какую из двух функций выполнять:

```
var_even = fn matchEven -> fn matchOdd -> matchEven
var_odd = fn matchEven -> fn matchOdd -> matchOdd
even_or_odd = fix (fn self -> fn variant -> 
	variant 
		(fn n -> if n = 0 then true else self var_odd (n - 1)) 
		(fn n -> if n = 1 then true else self var_even (n - 1))
)
even = even_or_odd var_even
odd = even_or_odd var_odd
```
